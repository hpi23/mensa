// WIRD UEBER DIE GNU KATZE INKLUDIERT

//
// Datentypen.
//

datentyp Speise auf Objekt {
    Zeichenkette Titel /
    Zeichenkette Beschreibung /
    Fließkommazahl Preis /
    Fließkommazahl Protein /
    Zeichenkette ZeitSlot /
};

//
// Konstanten.
//

setze Zahl MENSA_FILMUNI auf 9603;
setze Zahl MENSA_GRIEBNITZSEE auf 9601;
setze Zeichenkette MENSA_URL_GENERISCH auf "https://swp.webspeiseplan.de/index.php?token=55ed21609e26bbf68ba2b19390bf7961&model=menu&location=%d&languagetype=2&_=1699354619713";

//
// Funktionen.
//

funk Lade_Speiseangebot(Zahl Mensa_Ort / Zahl Tages_Verschiebung) ergibt Liste von Speise {
    setze Zeichenkette Körper auf "";
    setze Zahl Antwort_Zahl auf Http(
        "GET" /
        Formatiere(MENSA_URL_GENERISCH / Mensa_Ort) /
        "" /
        [
            erstelle {
                Zeichenkette Schlüssel auf "Referer" /
                Zeichenkette Wert auf "https://swp.webspeiseplan.de/menu" /
            }
        ] /
        &Körper /
    );

    falls Antwort_Zahl != 200 {
        Aufgeben(1);
    }

    setze Speicherbox Res auf Zergliedere_JSON(Körper) als Speicherbox;
    setze Liste von Speicherbox ContentRes auf Res.Nehmen("content") als Liste von Speicherbox;
    setze Zahl Zähler auf 0;

    setze Liste von Speise Resultat auf [];

    solange Zähler < ContentRes.Länge() {
        setze Speicherbox ResTemp auf ContentRes[Zähler];
        setze Liste von Speicherbox Gerichte auf ResTemp.Nehmen("speiseplanGerichtData") als Liste von Speicherbox;
        setze Speicherbox SpeiseplanAdvanced auf ResTemp.Nehmen("speiseplanAdvanced") als Speicherbox;
        setze Zeichenkette ZeitSlot auf SpeiseplanAdvanced.Nehmen("titel") als Zeichenkette;

        setze Zahl Gerichtzähler auf 0;
        solange (Gerichtzähler < Gerichte.Länge()) {
            setze Speicherbox Gericht auf Gerichte[Gerichtzähler];
            Gerichtzähler += 1;


            setze Speicherbox ZusatzInfos auf Gericht.Nehmen("zusatzinformationen") als Speicherbox;

            // DIES STIRBT MANCHMAL.
            setze Fließkommazahl Preis auf ZusatzInfos.Nehmen("mitarbeiterpreisDecimal2") als Fließkommazahl;

            setze Speicherbox GerichtInfos auf Gericht.Nehmen("speiseplanAdvancedGericht") als Speicherbox;
            setze Zeichenkette GerichtDatumRoh auf GerichtInfos.Nehmen("datum") als Zeichenkette;

            setze Zeichenkette GerichtDatum auf GerichtDatumRoh.Zertrenne("T")[0];

            setze Zahl Verwendeter_Tag auf Zeit().Kalendar_Tag + Tages_Verschiebung;
            setze Zeichenkette HeuteDatum auf Formatiere("%d-%2d-%2d" / Zeit().Jahr / Zeit().Monat / Verwendeter_Tag);
            Drucke(HeuteDatum);
            setze Zeichenkette Gerichtname auf GerichtInfos
                .Nehmen("gerichtname") als Zeichenkette;

            falls
                (GerichtDatum != HeuteDatum)
                || Gerichtname.Startet_Mit("Salatbuffet")
                || Gerichtname.Enthält("Relevo")
                || Gerichtname.Enthält("Wochenende!")
                || Gerichtname.Enthält("Preise pro 100 g")
                || Gerichtname.Enthält("Preis pro 100 g")
                || Gerichtname.Enthält("Preis pro 100g") { // WARUM??? Es gibt einfach beide!
                weitermachen;
            }

            Resultat.Hinzufügen(erstelle {
                Zeichenkette ZeitSlot auf ZeitSlot /
                Zeichenkette Titel auf Formatiere("Angebot %d" / Resultat.Länge() + 1) /
                Zeichenkette Beschreibung auf Gerichtname /
                Fließkommazahl Preis auf Preis /
                Zeichenkette ZeitSlot auf ZeitSlot /
            });

        }

        Zähler += 1;
    }

    Resultat
}

// ENDE IMPORTIERE
